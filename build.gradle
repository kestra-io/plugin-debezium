plugins {
    id 'java-library'
    id "idea"
    id "com.adarshr.test-logger" version "3.2.0" apply(false)
    id "com.github.johnrengelman.shadow" version "7.1.2" apply(false)
    id 'ru.vyarus.java-lib' version '2.1.0' apply(false)
    id 'ru.vyarus.github-info' version '1.4.0' apply(false)
    id 'signing'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id "com.github.ben-manes.versions" version "0.42.0"
    id 'net.researchgate.release' version '3.0.2'
}

def isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs.add("-parameters")
}

group "io.kestra.plugin"

subprojects {
    group "io.kestra.plugin"
    sourceCompatibility = 11

    repositories {
        mavenCentral()
        if (isBuildSnapshot) {
            maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
        }
    }

    apply plugin: "java-library"
    apply plugin: "idea"

    dependencies {
        // lombok
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"

        // micronaut
        annotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
        annotationProcessor "io.micronaut:micronaut-inject-java"
        annotationProcessor "io.micronaut:micronaut-validation"
        compileOnly platform("io.micronaut:micronaut-bom:$micronautVersion")
        compileOnly "io.micronaut:micronaut-inject"
        compileOnly "io.micronaut:micronaut-validation"
        compileOnly group: 'ch.qos.logback', name: 'logback-classic'

        // kestra
        compileOnly group: "io.kestra", name: "core", version: kestraVersion
    }
}

/**********************************************************************************************************************\
 * Test
 **********************************************************************************************************************/
subprojects {
    apply plugin: "com.adarshr.test-logger"

    test {
        useJUnitPlatform()
    }

    testlogger {
        theme "mocha-parallel"
        showExceptions true
        showFullStackTraces true
        showStandardStreams true
        showPassedStandardStreams false
        showSkippedStandardStreams true
    }

    dependencies {
        testAnnotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
        testAnnotationProcessor "io.micronaut:micronaut-inject-java"
        testImplementation platform("io.micronaut:micronaut-bom:$micronautVersion")
        testImplementation "io.micronaut.test:micronaut-test-junit5"

        // test deps needed only for to have a runner
        testImplementation group: "io.kestra", name: "core", version: kestraVersion
        testImplementation group: "io.kestra", name: "repository-memory", version: kestraVersion
        testImplementation group: "io.kestra", name: "runner-memory", version: kestraVersion
        testImplementation group: "io.kestra", name: "storage-local", version: kestraVersion

        // test
        testImplementation "org.junit.jupiter:junit-jupiter-engine"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testImplementation "org.hamcrest:hamcrest:2.2"
        testImplementation "org.hamcrest:hamcrest-library:2.2"
    }
}

/**********************************************************************************************************************\
 * Publish
 **********************************************************************************************************************/
nexusPublishing {
    repositoryDescription = "${project.group}:${rootProject.name}:${project.version}"
    useStaging = !isBuildSnapshot
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

subprojects { Project subproject ->
    apply plugin: "com.adarshr.test-logger"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "maven-publish"
    apply plugin: "signing"
    apply plugin: 'ru.vyarus.github-info'
    apply plugin: 'ru.vyarus.java-lib'

    def group

    if (subproject.name == "plugin-debezium") {
        subproject.description = 'Abstract debezium plugin for Kestra'
        group = project.group + ".debezium"
    } else {
        subproject.description = subproject.name.substring(16, 17).toUpperCase() + subproject.name.substring(17)  + ' debezium plugin for Kestra'
        group = project.group + ".debezium." + subproject.name.replace('plugin-debezium-', '')
    }

    github {
        user 'kestra-io'
        repository 'plugin-debezium'
        license 'Apache'
    }

    if (subproject.name != "plugin-debezium") {
        shadowJar {
            dependsOn(":plugin-debezium:shadowJar")
        }
    }

    jar {
        manifest {
            attributes(
                    "X-Kestra-Title": subproject.name,
                    "X-Kestra-Group": group,
                    "X-Kestra-Description": subproject.description,
                    "X-Kestra-Version": project.version
            )
        }
    }

    pom {
        developers {
            developer {
                id "tchiotludo"
                name "Ludovic Dehon"
            }
        }
    }

    signing {
        sign publishing.publications.maven
    }

    shadowJar {
        archiveClassifier.set(null)
        mergeServiceFiles()
        if (subproject.name != "plugin-debezium") {
            dependsOn(project(":plugin-debezium").tasks.named("shadowJar"))
        }
    }
}

/**********************************************************************************************************************\
 * Version
 **********************************************************************************************************************/
release {
    preCommitText = 'chore(version):'
    preTagCommitMessage = 'update to version'
    tagCommitMessage = 'tag version'
    newVersionCommitMessage = 'update snapshot version'
    tagTemplate = 'v${version}'
    buildTasks = ['classes']
    git {
        requireBranch.set('master')
    }
}

/**********************************************************************************************************************\
 * Dev
 **********************************************************************************************************************/
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
